## Terms export query v1.2

{% include 'sparql/_namespaces.sparql' %}

SELECT ?dui ?active
      (GROUP_CONCAT(DISTINCT ?pterm ; separator='~') AS ?terms)
      (GROUP_CONCAT(DISTINCT ?ptermx ; separator='~') AS ?termsx)
      (GROUP_CONCAT(DISTINCT ?nterm ; separator='~') AS ?nterms)
      (GROUP_CONCAT(DISTINCT ?ntermx ; separator='~') AS ?ntermsx)
WHERE {
  ?d rdf:type ?dtype .
  FILTER(?dtype IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))

  # Assume absence of meshv:active as true
  BIND(IF(EXISTS { ?d meshv:active false }, false, true) AS ?active) .

  ?d meshv:identifier ?dui .
  ?d meshv:preferredConcept ?c .

  OPTIONAL {
    ?c meshv:term ?pt .
    ?pt meshv:prefLabel ?pterm
  }

  OPTIONAL {
    ?c mesht:term ?ptt .
    ?ptt mesht:prefLabel ?ptermx
  }

  OPTIONAL {
    ?d meshv:concept ?ct .
    ?ct meshv:term|meshv:preferredTerm ?nt .
    ?nt meshv:prefLabel ?nterm
  }

  OPTIONAL {
    ?d meshv:concept|mesht:concept ?cct .
    ?cct mesht:term|mesht:preferredTerm ?ntx .
    ?ntx mesht:prefLabel ?ntermx
  }
}
GROUP BY ?dui ?active
#LIMIT 100
