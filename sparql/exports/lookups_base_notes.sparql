PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX meshv:  <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesh:   <http://id.nlm.nih.gov/mesh/>
PREFIX text:   <http://jena.apache.org/text#>
PREFIX meshx:  <http://mesh.medvik.cz/link/>
PREFIX mesht:  <http://www.medvik.cz/schema/mesh/vocab/#>

SELECT ?dui ?cui ?scn ?scnt ?an ?tan ?cx ?tcx ?hn ?thn ?ol ?pm
       (GROUP_CONCAT(DISTINCT ?prev ; separator='~') AS ?pi)
WHERE {
  VALUES ?dui { "D001794" "D007676" "D009203" "D005585" "D008099" "Q000032" "D013827" "D000098346" }
  ?d meshv:identifier ?dui .
  ?d rdf:type ?dtype .
  FILTER(?dtype IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
  ?d meshv:preferredConcept ?c .
  ?c meshv:identifier ?cui .
  OPTIONAL { ?c meshv:scopeNote ?scn }
  OPTIONAL { ?c mesht:scopeNote ?scnt }
  ### DESC_NOTES = annotation,considerAlso,historyNote,onlineNote,publicMeSHNote,translatorsNote,previousIndexing*
  ### Translate:   annotation,considerAlso,historyNote
  OPTIONAL { ?d meshv:annotation ?an }
  OPTIONAL { ?d mesht:annotation ?tan }
  OPTIONAL { ?d meshv:considerAlso ?cx }
  OPTIONAL { ?d mesht:considerAlso ?tcx }
  OPTIONAL { ?d meshv:historyNote ?hn }
  OPTIONAL { ?d mesht:historyNote ?thn }
  OPTIONAL { ?d meshv:onlineNote ?ol }
  OPTIONAL { ?d meshv:publicMeSHNote ?pm }
  OPTIONAL { ?d meshv:previousIndexing ?prev }
}
GROUP BY ?dui ?cui ?scn ?scnt ?an ?tan ?cx ?tcx ?hn ?thn ?ol ?pm
LIMIT 10
