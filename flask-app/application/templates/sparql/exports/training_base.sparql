## MeSH training export - base query v1.3

{% include 'sparql/_namespaces.sparql' %}

SELECT ?status ?tstatus ?tt ?dui ?cui ?den ?trx
       (GROUP_CONCAT(DISTINCT ?pterm ; separator='~') AS ?terms)
       (GROUP_CONCAT(DISTINCT ?ptermx ; separator='~') AS ?termsx)
       (GROUP_CONCAT(DISTINCT ?nterm ; separator='~') AS ?nterms)
       (GROUP_CONCAT(DISTINCT ?ntermx ; separator='~') AS ?ntermsx)
       (GROUP_CONCAT(DISTINCT ?trn ; separator='~') AS ?trns)
WHERE {
  {
    # MH block
    ?d rdf:type ?dtype .
    FILTER(?dtype IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
    ?d meshv:identifier ?dui .
    # Assume absence of meshv:active as true
    BIND(IF(EXISTS { ?d meshv:active false }, false, true) AS ?status)
    ?d meshv:preferredConcept ?c .
    ?c meshv:identifier ?cui .
    ?c meshv:preferredTerm ?t .
    ?t meshv:prefLabel ?den .
    OPTIONAL {
      ?c mesht:preferredTerm ?tp .
      ?tp mesht:prefLabel ?trx .
      OPTIONAL { ?tp mesht:active ?tstatus }
    }
    OPTIONAL {
      ?c meshv:term ?pt .
      ?pt meshv:prefLabel ?pterm
    }
    OPTIONAL {
      ?c mesht:term ?ptt .
      ?ptt mesht:prefLabel ?ptermx
    }
    OPTIONAL {
      ?d meshv:concept ?ct .
      ?ct meshv:term|meshv:preferredTerm ?nt .
      ?nt meshv:prefLabel ?nterm
    }
    OPTIONAL {
      ?d meshv:concept|mesht:concept ?cct .
      ?cct mesht:term|mesht:preferredTerm ?ntx .
      ?ntx mesht:prefLabel ?ntermx
    }
    OPTIONAL {
       ?d meshv:treeNumber ?tn .
       ?tn rdfs:label ?trn .
       FILTER(!STRSTARTS(?trn, "[OBSOLETE]"))
    }
    BIND("MH" AS ?tt)
  }
  UNION
  {
    # PEP block
    ?d rdf:type ?dtype .
    FILTER(?dtype IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
    ?d meshv:identifier ?dui .
    # Assume absence of meshv:active as true
    BIND(IF(EXISTS { ?d meshv:active false }, false, true) AS ?status)
    ?d meshv:concept|mesht:concept ?c .
    ?c meshv:identifier ?cui .
    ?c meshv:preferredTerm ?t .
    ?t meshv:prefLabel ?den .
    OPTIONAL {
      ?c mesht:preferredTerm ?tp .
      ?tp mesht:prefLabel ?trx .
      OPTIONAL { ?tp mesht:active ?tstatus }
    }
    OPTIONAL {
      ?c meshv:term ?pt .
      ?pt meshv:prefLabel ?pterm
    }
    OPTIONAL {
      ?c mesht:term ?ptt .
      ?ptt mesht:prefLabel ?ptermx
    }
    OPTIONAL {
       ?d meshv:treeNumber ?tn .
       ?tn rdfs:label ?trn .
       FILTER(!STRSTARTS(?trn, "[OBSOLETE]"))
    }
    BIND("" AS ?nterm)
    BIND("" AS ?ntermx)
    BIND("PEP" AS ?tt)
  }
  UNION
  {
    # SCR block
    ?d rdf:type ?dtype .
    FILTER(?dtype IN(meshv:SCR_Anatomy, meshv:SCR_Disease, meshv:SCR_Chemical, meshv:SCR_Organism, meshv:SCR_Population, meshv:SCR_Protocol))
    ?d meshv:identifier ?dui .
    # Assume absence of meshv:active as true
    BIND(IF(EXISTS { ?d meshv:active false }, false, true) AS ?status)
    ?d meshv:preferredConcept ?c .
    ?c meshv:identifier ?cui .
    ?c meshv:preferredTerm ?t .
    ?t meshv:prefLabel ?den .
    ?d meshv:preferredMappedTo ?tp .
    ?tp rdfs:label ?trx .
    OPTIONAL { ?tp meshv:active ?tstatus }
    OPTIONAL {
      ?c meshv:term ?pt .
      ?pt meshv:prefLabel ?pterm
    }
    OPTIONAL {
      ?c mesht:term ?ptt .
      ?ptt mesht:prefLabel ?ptermx
    }
    OPTIONAL {
       ?d meshv:preferredMappedTo ?dd .
       ?dd meshv:treeNumber ?tn .
       ?tn rdfs:label ?trn .
    }
    BIND("" AS ?nterm)
    BIND("" AS ?ntermx)
    BIND("SCR" AS ?tt)
  }
}
GROUP BY ?status ?tstatus ?tt ?dui ?cui ?den ?trx
#LIMIT 100
