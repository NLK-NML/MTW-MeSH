PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX meshv:  <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesh:   <http://id.nlm.nih.gov/mesh/>
PREFIX text:   <http://jena.apache.org/text#>
PREFIX mesht:  <http://www.medvik.cz/schema/mesh/vocab/#>
PREFIX meshx: <http://mesh.medvik.cz/link/>


## Tree display query v1.1
## endpoint: http://localhost:3030/mesh/query

SELECT distinct ?tn ?label ?d ?type ?val ?active ?scn ?scnt ?ntx {
{SELECT * {
{SELECT ?tn ?d ?c
WHERE {
   mesh:D017282 meshv:treeNumber ?treeNum .
   ?treeNum meshv:parentTreeNumber ?parent .
   BIND(CONCAT(str(?parent),'.???') as ?q) .
   (?tn ?score) text:query (?q) .
   ?d meshv:treeNumber ?tn .
   ?d meshv:preferredConcept ?c .
}} UNION {
SELECT ?tn ?d ?c
WHERE {
   mesh:D017282 meshv:treeNumber ?treeNum .
   ?treeNum meshv:parentTreeNumber+ ?tn .
   ?d meshv:treeNumber ?tn .
   ?d meshv:preferredConcept ?c .
}} UNION {
SELECT ?tn ?d ?c
WHERE {
   mesh:D017282 meshv:treeNumber ?treeNum .
   BIND(CONCAT(str(?treeNum),'.???') as ?q) .
   (?tn ?score) text:query (?q) .
   ?d meshv:treeNumber ?tn .
   ?d meshv:preferredConcept ?c .
}} UNION {
SELECT ?tn ?d ?c
WHERE {
   mesh:D017282 meshv:treeNumber ?tn .
   ?d meshv:treeNumber ?tn .
   ?d meshv:preferredConcept ?c .
}}
  }
ORDER BY ?tn LIMIT 500
}
  ?d rdfs:label ?label .
  ?d rdf:type ?type .

  # Assume absence of meshv:active as true
  BIND(IF(EXISTS { ?d meshv:active false }, false, true) AS ?active) .

   OPTIONAL {
        ?c mesht:preferredTerm ?id .
        ?id mesht:prefLabel ?val
        }
    OPTIONAL {?c meshv:scopeNote ?scnv .
              BIND('YES' as ?scn)
             }
    OPTIONAL {?c mesht:scopeNote ?scntv
              BIND('YES' as ?scnt)
             }
    OPTIONAL {?c mesht:notTranslatable ?ntx}
    FILTER (lang(?label) = 'en')
}

