PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX meshv:  <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesh:   <http://id.nlm.nih.gov/mesh/>
PREFIX text:   <http://jena.apache.org/text#>
PREFIX meshx:  <http://mesh.medvik.cz/link/>
PREFIX mesht:  <http://www.medvik.cz/schema/mesh/vocab/#>

## MeSH training export query v1.1

SELECT ?status ?tstatus ?dui ?cui ?den ?trx ?tt
       (GROUP_CONCAT(DISTINCT ?pterm ; separator='~') AS ?terms)
       (GROUP_CONCAT(DISTINCT ?ptermx ; separator='~') AS ?termsx)
       (GROUP_CONCAT(DISTINCT ?nterm ; separator='~') AS ?nterms)
       (GROUP_CONCAT(DISTINCT ?ntermx ; separator='~') AS ?ntermsx)
       (GROUP_CONCAT(DISTINCT ?trn ; separator='~') AS ?trns)
WHERE {
  {
    # MH block
    VALUES ?dui { "D001794" "D007676" "D009203" "D005585" "D008099" "Q000032" "D013827" "D000098346" }
    ?d meshv:identifier ?dui .
    ?d rdf:type ?dtype .
    FILTER(?dtype IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
    # Assume absence of meshv:active as true
    BIND(IF(EXISTS { ?d meshv:active false }, false, true) AS ?status) .
    ?d meshv:preferredConcept ?c .
    ?c meshv:identifier ?cui .
    ?c meshv:preferredTerm ?t .
    ?t meshv:prefLabel ?den .
    OPTIONAL {
      ?c mesht:preferredTerm ?tp .
      ?tp mesht:prefLabel ?trx .
      OPTIONAL { ?tp mesht:active ?tstatus }
    }
    OPTIONAL {
      ?c meshv:term ?pt .
      ?pt meshv:prefLabel ?pterm
    }
    OPTIONAL {
      ?c mesht:term ?ptt .
      ?ptt mesht:prefLabel ?ptermx
    }
    OPTIONAL {
      ?d meshv:concept ?ct .
      ?ct meshv:term|meshv:preferredTerm ?nt .
      ?nt meshv:prefLabel ?nterm
    }
    OPTIONAL {
      ?d meshv:concept|mesht:concept ?cct .
      ?cct mesht:term|mesht:preferredTerm ?ntx .
      ?ntx mesht:prefLabel ?ntermx
    }
    OPTIONAL {
       ?d meshv:treeNumber ?tn .
       ?tn rdfs:label ?trn .
    }
    BIND("MH" AS ?tt)
  }
  UNION
  {
    # PEP block
    VALUES ?dui { "D001794" "D007676" "D009203" "D005585" "D008099" "Q000032" "D013827" "D000098346" }
    ?d meshv:identifier ?dui .
    ?d rdf:type ?dtype .
    FILTER(?dtype IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
    ?d meshv:concept|mesht:concept ?c .
    ?c meshv:identifier ?cui .
    ?c meshv:preferredTerm ?t .
    ?t meshv:prefLabel ?den .
    OPTIONAL {
      ?c mesht:preferredTerm ?tp .
      ?tp mesht:prefLabel ?trx .
      OPTIONAL { ?tp mesht:active ?tstatus }
    }
    OPTIONAL {
      ?c meshv:term ?pt .
      ?pt meshv:prefLabel ?pterm
    }
    OPTIONAL {
      ?c mesht:term ?ptt .
      ?ptt mesht:prefLabel ?ptermx
    }
    OPTIONAL {
       ?d meshv:treeNumber ?tn .
       ?tn rdfs:label ?trn .
    }
    BIND("" AS ?nterm)
    BIND("" AS ?ntermx)
    BIND("PEP" AS ?tt)
  }
}
GROUP BY ?status ?tstatus ?dui ?cui ?den ?trx ?tt
ORDER BY ?dui
LIMIT 100
