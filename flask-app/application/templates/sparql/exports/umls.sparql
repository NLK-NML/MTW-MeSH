## UMLS export query v1.1

{% include 'sparql/_namespaces.sparql' %}

select * {
{
SELECT distinct ?status ?tstatus ?dui ?cui ?lang ?tty ?str ?tui ?scn
WHERE {
  BIND('MH' as ?tty)
  BIND('{{ lang_umls }}' as ?lang)
  ?d rdf:type ?type
  FILTER(?type IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
  ?d meshv:identifier ?dui .

  # Assume absence of meshv:active as true
  BIND(IF(EXISTS { ?d meshv:active false }, false, true) AS ?status) .

  ?d meshv:preferredConcept ?c .
  ?c meshv:identifier ?cui .
  ?c mesht:preferredTerm ?t .
  ?t mesht:prefLabel ?val .
  BIND(STR(?val) AS ?str)
  OPTIONAL { ?t mesht:identifier ?tui }
  OPTIONAL { ?t mesht:active ?tstatus }
  OPTIONAL {
      ?c mesht:scopeNote ?scnval
      BIND(STR(?scnval) AS ?scn)
      }
}
#LIMIT 10
} UNION {
SELECT distinct ?status ?tstatus ?dui ?cui ?lang ?tty ?str ?tui
(group_concat(?scnt;separator="|") as ?scn)
WHERE {
  BIND('PEP' as ?tty)
  BIND('{{ lang_umls }}' as ?lang)
  ?d rdf:type ?type
  FILTER(?type IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
  ?d meshv:identifier ?dui .

  BIND(IF(EXISTS { ?d meshv:active false }, false, true) AS ?status) .

  ?d meshv:concept|mesht:concept ?c .
  OPTIONAL { ?c meshv:identifier|mesht:identifier ?cui }
  ?c mesht:preferredTerm ?t .
  ?t mesht:prefLabel ?val .
  BIND(STR(?val) AS ?str)
  OPTIONAL { ?t mesht:identifier ?tui }
  OPTIONAL { ?t mesht:active ?tstatus }
  OPTIONAL {
      ?c mesht:scopeNote ?scnval
      BIND(STR(?scnval) AS ?scnt)
      }
}
GROUP BY ?status ?tstatus ?dui ?cui ?lang ?tty ?str ?tui
#LIMIT 10
} UNION {
SELECT distinct ?status ?tstatus ?dui ?cui ?lang ?tty ?str ?tui
WHERE {
  BIND('ET' as ?tty)
  BIND('{{ lang_umls }}' as ?lang)
  ?d rdf:type ?type
  FILTER(?type IN(meshv:TopicalDescriptor,meshv:GeographicalDescriptor,meshv:PublicationType,meshv:CheckTag,meshv:Qualifier))
  ?d meshv:identifier ?dui .

  BIND(IF(EXISTS { ?d meshv:active false }, false, true) AS ?status) .

  ?d meshv:preferredConcept|meshv:concept|mesht:concept ?c .
  OPTIONAL { ?c meshv:identifier|mesht:identifier ?cui }
  ?c mesht:term ?t .
  ?t mesht:prefLabel ?val .
  BIND(STR(?val) AS ?str)
  OPTIONAL { ?t mesht:identifier ?tui }
  OPTIONAL { ?t mesht:active ?tstatus }
}
#LIMIT 10
}
}
